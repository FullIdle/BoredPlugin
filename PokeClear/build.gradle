plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
    id 'eclipse'
    id 'idea'
}

group = 'com.github.fullidle.boredplugin.pokeclear.Main'
version = '1.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {url "https://maven.mohistmc.com/"}
}

dependencies {
    implementation project(path: ':common')
    compileOnly 'com.mohistmc:mohistdev:1.20.1'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    compileOnly 'org.spigotmc:spigot:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    compileOnly rootProject.fileTree('librarys/[Server]Pixelmon-1.20.1-9.2.2-server.jar')
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

processResources {
    setFilteringCharset("UTF-8")
    expand project.properties
    doLast {
        if (project.rootProject.hasProperty('isRootProject')) {
            if (project.rootProject.property('isRootProject')){
                def sourceDir = file("$project.buildDir/resources/main")
                renameAllFile(sourceDir)
            }
        }
    }
}

def renameAllFile(File folder){
    for (final def f in folder.listFiles()) {
        if (f.isDirectory()) {
            renameAllFile(f)
        }else{
            if ('plugin.yml' != f.name){
                def renameFile = file("${f.parentFile}/${project.name}-${f.name}")
                f.renameTo(renameFile)
            }
        }
    }
}

shadowJar {
    mergeServiceFiles()
    archiveBaseName.set(archivesBaseName)
    archiveVersion.set(version)
    manifest {
        attributes 'Main-Class': project.group
    }
}